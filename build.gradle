plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'idea'
}

group 'com.chess'
version '1.0-SNAPSHOT'

sourceCompatibility = '1.8'
mainClassName = 'com.chess.Application'


repositories {
    mavenCentral()
}

dependencies {
    def junitVersion = "5.5.2"

    //loombook
    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'

    implementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'


    testCompile group: 'junit', name: 'junit', version: '4.12'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

test {
    useJUnitPlatform()
    reports.html.enabled = false
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.setEnabled true
        html.destination = file("$buildDir/reports/coverage")
    }

}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }
}